use units::imperial

# Smallest addressable element on a digital display
dimension Pixel

@name("Pixel")
@url("https://en.wikipedia.org/wiki/Pixel")
@metric_prefixes
@aliases(pixels, px: short)
unit pixel: Pixel

@name("Pixels per inch")
@url("https://en.wikipedia.org/wiki/Pixels_per_inch")
unit ppi: Pixel / Length = pixel / inch


# Smallest possible output resolution on a printing device
dimension Dot

@name("Dot")
@url("https://en.wikipedia.org/wiki/Dots_per_inch")
@aliases(dots)
unit dot: Dot

@name("Dots per inch")
@url("https://en.wikipedia.org/wiki/Dots_per_inch")
unit dpi: Dot / Length = dots / inch


# A single image in a (video) sequence
dimension Frame

@name("Frame")
@url("https://en.wikipedia.org/wiki/Frame_rate")
@aliases(frames)
unit frame: Frame

@name("Frames per second")
@url("https://en.wikipedia.org/wiki/Frame_rate")
unit fps: Frame / Time = frame / second


# A separate or limited portion or quantity of something
dimension Piece

@name("Piece")
@aliases(pieces)
unit piece: Piece


# A human being
dimension Person

@name("Person")
@aliases(persons, people, capita)
unit person: Person
